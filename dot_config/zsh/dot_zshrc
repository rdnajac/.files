# .zshrc - my zsh configuration
# echo 'sourcing zshrc'
if [ -f ~/.bash_aliases ]; then
	. ~/.bash_aliases
fi

setopt interactivecomments
# history {{{
setopt APPEND_HISTORY
setopt INC_APPEND_HISTORY SHARE_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_REDUCE_BLANKS

export HISTFILE="${ZDOTDIR:-$HOME}/.zsh_history"
export HISTSIZE=10000
export SAVEHIST=10000

# Don't pollute $HOME with history files
alias wget='wget --no-hsts'
export LESSHISTFILE=-
# }}}

# Zsh Line Editor (ZLE) configuration {{{
# https://zsh.sourceforge.io/Guide/zshguide04.html#l76
# https://zsh.sourceforge.io/Doc/Release/Zsh-Line-Editor.html#Zsh-Line-Editor

# Run command without clearing input with `<M-Enter>`
bindkey '\e\r' accept-and-hold
# XXX: Tested on alacrity, but not on other terminals

autoload -Uz up-line-or-beginning-search down-line-or-beginning-search

zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

bindkey '^[[A'  up-line-or-beginning-search    # Arrow up
bindkey '^[OA'  up-line-or-beginning-search
bindkey '^[[B'  down-line-or-beginning-search  # Arrow down
bindkey '^[OB'  down-line-or-beginning-search

bindkey "^[[1;2D" backward-word     # Shift + Left
bindkey "^[[1;2C" forward-word      # Shift + Right
# bindkey "^[[1;3D" backward-word       # Alt + Left (most terminals)
# bindkey "^[[1;3C" forward-word        # Alt + Right (most terminals)

bindkey "^[[1;4D" beginning-of-line # Shift + Alt + Left
bindkey "^[[1;4C" end-of-line       # Shift + Alt + Right

# https://zsh.sourceforge.io/Guide/zshguide04.html#l87
export WORDCHARS=${WORDCHARS//[&.;\/]}

# don't disable bracketed paste, do this instead
zle_highlight=('paste:none')
# }}}

# Enable zsh native completion {{{
fpath=(/opt/homebrew/share/zsh/site-functions $fpath)
autoload -Uz compinit && compinit

zstyle ':completion:*:*:scp:*' tag-order files
zstyle ':completion:*:scp:*' remote-access yes
# zstyle ':completion:*:*:(scp|rsync):*' tag-order files
# zstyle ':completion:*:(scp|rsync):*' remote-access yes

# Completion caching for speed
# Follow XDG Base Directory spec for cache
ZSH_CACHE_DIR="${XDG_CACHE_HOME:-$HOME/.cache}/zsh"

# Create if missing
[[ ! -d $ZSH_CACHE_DIR ]] && mkdir -p "$ZSH_CACHE_DIR"

# Tell completion system to cache there
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $ZSH_CACHE_DIR

# (Optional) Enable bashcompinit only if needed
# Only if you later need bash-style `complete -C` for some weird CLI tools
# autoload -Uz bashcompinit
# bashcompinit
# }}}

# Load additional zsh configs in this dir
for f in $ZDOTDIR/*.zsh(N); do
	. "$f"
done

# Loop over all files in env dir and source them if the executable exists
for f in "${XDG_CONFIG_HOME:-$HOME/.config}"/env/*.sh(N); do
    (( $+commands[${f:t:r}] )) && . "$f"
done

# set up Ruby environment
# source /opt/homebrew/opt/chruby/share/chruby/chruby.sh
# source /opt/homebrew/opt/chruby/share/chruby/auto.sh
# chruby ruby-3.1.3 # run chruby to see actual version

# silence erros in case we don't have this
micromamba activate cbmf 2>/dev/null || true
eval "$(register-python-argcomplete cbmf 2>/dev/null)" || true

echo "The computing scientist's main challenge is not to get confused by the complexities of his own making."
